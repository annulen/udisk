#!/usr/bin/env perl

use strict;
use warnings;

my @commands_ordered = (
    status => "Prints high-level status (default command)",
    mount => "Mounts a filesystem",
    umount => "Unmounts a filesystem",
    wait => "Waits for new device to appear then prints status",
    help => "Shows this information"
);
my %commands = @commands_ordered;

sub help {
    print "Usage: $0 [COMMAND]\n\n";
    print "Commands:\n";
    for(my $i = 0; $i < @commands_ordered; $i += 2) {
        my ($c, $h) = @commands_ordered[$i, $i + 1];
        printf "  %-15s  %s\n", $c, $h;
    }
}

sub usage {
    print "@_\n";
    help();
    exit(1);
}

sub status_processHeader {
    my $pipe = shift;

    # MODEL REVISION SERIAL DEVICE
    # ----------------------------
    my $hdr = <$pipe>;
    $hdr =~ /^MODEL\s+(REVISION)\s+(SERIAL)\s+(DEVICE)/ or return "";

    my @l = map { $-[$_] - $-[$_-1] } 1..3;
    my $in_fmt = sprintf "A%sA%sA%sA*", @l;
    my $out_fmt = sprintf "A%sA*", $l[0];

    $_ = <$pipe>;
    /^-*$/ or return "";
    return $in_fmt, $out_fmt
}

sub status {
    my $cmd = "udisksctl status";
    open my $pipe, "-|", $cmd or die "$cmd failed: $!";

    my ($in_fmt, $out_fmt) = status_processHeader($pipe, $cmd)
        or usage "Unrecognized output format for `$cmd'";

    while (<$pipe>) {
        my @fields = unpack $in_fmt;
        my $dev = pop @fields;
        my @devs = glob("/dev/$dev*");
        @devs >= 1 or die "/dev/$dev not found";
        print pack $out_fmt, $fields[0], "$devs[0]\n";
        for my $i (1..$#devs) {
            print pack $out_fmt, "", "$devs[$i]\n";
        }
    }
}

sub mount {
    my $cmd = "udisksctl mount -b";
    for my $dev (@_) {
        print "$cmd $dev\n";
        system("$cmd $dev");
        print "Exit code: $?\n";
        print "\n";
    }
}

sub umount {
    my $cmd = "udisksctl unmount -b";
    for my $dev (@_) {
        print "$cmd $dev\n";
        system("$cmd $dev");
        print "Exit code: $?\n";
        print "\n";
    }
}

sub wait {
    my $cmd = "udisksctl monitor";
    my $pid = open my $pipe, "-|", $cmd or die "$cmd failed: $!";
    while (<$pipe>) {
        print;
        last if (/^[0-9.:]+: Added /);
    }
    print "Stopping monitor...\n";
    kill 9, $pid;
    print "\n";
    status();
}

sub complete {
    my $comp_line = shift;
    open my $log, '>>', 'complete.log';
    print $log "$comp_line ~~~ $0\n";
    return if $comp_line !~ /^$0 (.*)$/;
    print $log "$comp_line ~~~ $1\n";
}

my $cmd = shift @ARGV // "status";

# Hidden command for completion
if ($cmd eq 'complete') {
    scalar @ARGV == 2 or usage "Unknown command `$cmd'";

    my ($comp_line, $comp_point) = @ARGV;
    my $len = length $comp_line;
    if ($len == $comp_point) {
        complete($comp_line);
    }
    #    for(my $i = 0; $i < @commands_ordered; $i += 2) {
    #        print $commands_ordered[$i], " ";
    #    }
    exit(0);
}

exists $commands{$cmd} or usage "Unknown command `$cmd'";
{
    no strict 'refs';
    &$cmd(@ARGV);
}
